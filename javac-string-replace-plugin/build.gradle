import java.nio.file.Files
import java.nio.file.Paths

def javaHome = Paths.get(System.getenv("JAVA_HOME") + "/../lib/tools.jar")
def jvmHome = Paths.get(System.getProperty("java.home") + "/../lib/tools.jar")
def toolsJar = toolsJarPath ?: Files.exists(javaHome) ? javaHome : Files.exists(jvmHome) ? jvmHome : null

def jdkVersion = (int) Float.parseFloat(System.getProperty("java.class.version"))
def isModuleJdk = jdkVersion >= 53  // class file 53 = Jva SE 9


dependencies {
    compile('com.google.guava:guava:27.0.1-jre')
    // Required for Java 8 or smaller
    if (!useModule) {
        compileOnly(
                files(toolsJar)
        )
        testRuntime(
                files(toolsJar)
        )
    }
}

if (useModule) {
    compileJava {
        options.compilerArgs = [
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.code.type=ALL-UNNAMED',
                '--add-exports', 'jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED'
        ]
    }
}

if (addExportsToJavadoc && isModuleJdk) {
    javadoc {
        options.addMultilineStringsOption("-add-exports").setValue([
                'jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.code.type=ALL-UNNAMED',
                'jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED'
        ])
    }
}
